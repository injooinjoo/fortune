plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.beyond.fortune"
    compileSdk = 36  // Updated for plugin compatibility
    ndkVersion = "28.2.13676358"  // Required by integration_test and speech_to_text

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    defaultConfig {
        applicationId = "com.beyond.fortune"
        minSdkVersion = 26  // Required by health plugin
        targetSdk = 35  // Updated for Google Play Store API level 35 requirement (Nov 2024)
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        
        // Multidex support for methods count over 64K
        multiDexEnabled true
        
        // ProGuard rules for release builds
        consumerProguardFiles 'proguard-rules.pro'
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for release build
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            
            // Production signing config
            signingConfig signingConfigs.release
            
            // Production build configuration
            buildConfigField "String", "ENVIRONMENT", "\"production\""
            
            // Enable R8 for better code optimization
            shrinkResources true
        }
        
        debug {
            // Debug build configuration
            buildConfigField "String", "ENVIRONMENT", "\"development\""
            // applicationIdSuffix ".debug"  // Temporarily disabled for Firebase
            debuggable true
        }
        
        // staging {
        //     // Staging build configuration
        //     initWith buildTypes.release
        //     buildConfigField "String", "ENVIRONMENT", "\"staging\""
        //     applicationIdSuffix ".staging"
        //     matchingFallbacks = ['release']
        // }
    }
}

flutter {
    source = "../.."
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'

    // WorkManager for background widget refresh
    implementation "androidx.work:work-runtime-ktx:2.9.1"

    // Flutter 3.38 plugin registrant can include these dev plugins in release registrant;
    // keep release classpath aligned to avoid javac "package ... does not exist" failures.
    releaseImplementation project(':flutter_native_splash')
    releaseImplementation project(':integration_test')
}
