pluginManagement {
    def flutterSdkPath = {
        def properties = new Properties()
        file("local.properties").withInputStream { properties.load(it) }
        def flutterSdkPath = properties.getProperty("flutter.sdk")
        assert flutterSdkPath != null, "flutter.sdk not set in local.properties"
        return flutterSdkPath
    }()

    includeBuild("$flutterSdkPath/packages/flutter_tools/gradle")

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.9.1" apply false
    id "org.jetbrains.kotlin.android" version "2.1.0" apply false
    id "com.google.gms.google-services" version "4.4.0" apply false
}

include ":app"

// Fix for packages missing namespace (AGP 8.0+ compatibility)
// Also fix compileSdk for old plugins causing lStar error
gradle.afterProject { project ->
    if (project.plugins.hasPlugin('com.android.library')) {
        // Force compileSdk 34 for all library plugins (fixes lStar attribute error)
        if (project.android.compileSdkVersion?.toString()?.replace('android-', '')?.toInteger() < 34) {
            project.android.compileSdkVersion = 34
        }
        // Fix missing namespace
        if (project.android.namespace == null) {
            def manifest = file("${project.projectDir}/src/main/AndroidManifest.xml")
            if (manifest.exists()) {
                def packageValue = new XmlSlurper().parse(manifest).@package.text()
                if (packageValue) {
                    project.android.namespace = packageValue
                }
            }
        }
    }
}
